plugins {
    id("java-library")
    id("xyz.jpenilla.run-paper") version "3.0.2"
    id("com.gradleup.shadow") version "9.4.2"
}

repositories {
    mavenCentral()
    maven("https://repo.papermc.io/repository/maven-public/")
    maven("https://jitpack.io")
    maven("https://repo.glaremasters.me/repository/towny/")
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.8-R0.1-SNAPSHOT")

    compileOnly("org.spongepowered:configurate-gson:4.2.0")

    compileOnly("com.github.jwkerr:Bottlet:fc8a8adfde")
    compileOnly("com.palmergames.bukkit.towny:towny:0.103.0.0")
    implementation("ca.spottedleaf:concurrentutil:0.0.10") {
        exclude("net.java.dev.jna:jna")
        exclude("it.unimi.dsi:fastutil")
    }
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

tasks {
    runServer {
        minecraftVersion("1.21.11")
        jvmArgs("-Xms2G", "-Xmx2G", "-Dcom.mojang.eula.agree=true")

        downloadPlugins {
            modrinth("bottlet", "alpha-0.1.3")
            modrinth("towny", "0.103.0.0")
        }
    }

    processResources {
        val props = mapOf("version" to version, "description" to project.description)
        filesMatching("plugin.yml") {
            expand(props)
        }
    }

    shadowJar {
        archiveClassifier.set("")
        minimize()

        relocate("ca.spottedleaf.concurrentutil", "au.lupine.hopplet.libs.concurrentutil")
        exclude("com/sun/jna/**")
        exclude("it/unimi/dsi/fastutil/**")
    }
}
